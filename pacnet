#!/usr/bin/perl
use JSON::XS;
use Term::ANSIColor;
use Getopt::Long;
use LWP::Simple;
use File::Slurp 'slurp';
use 5.012;

my @packages; #here be database
my $dburl  = 'http://pacnet.karbownicki.com/api/packages/';
my $dbfile = $ENV{'HOME'} . '/.config/pacnet.db';

sub printpackage {
    my $p = shift;
    say colored('*', 'green'), ' ',
        $p->{'category__name'} .'/',
        colored($p->{'name'}, 'bold'), "\n     ",
        colored("Version:    \t", 'green'), $p->{'version'}, "\n     ",
        colored("Description:\t", 'green'), $p->{'description'}, "\n";
}

sub packagestring {
    my $p = shift;
    return $p->{'category__name'} . '/' . $p->{'name'};
}

sub getdb {
    unless(getstore($dburl, $dbfile) == RC_OK) {
        say "Failed downloading the package database";
        exit 1;
    }
}

sub readdb {
    getdb unless -f $dbfile;
    my $json = slurp $dbfile;
    @packages = @{decode_json($json)};
}

sub usage {
    print <<'END';
pacnet usage:
  -h (--help)  => show usage information
  -s (--sync)  => synchronise package database
  <regexp>     => search for a package matching <regexp>
END
}

sub MAIN {
    my $res = GetOptions(
        "s|sync" => sub { getdb },
        "h|help" => sub { usage },
    );

    readdb;

    if (scalar @ARGV) {
        printpackage($_)
            for grep { packagestring($_) =~ $ARGV[0] } @packages;
    }
}

MAIN;

# vim: ft=perl
